type User {
  displayName: String,
  email: String,
  photoURL: String,
  uid: String,
  billingAddress: String | Null,
  shippingAddress: String | Null,
}

type Role {
  isAdmin: Boolean,
  isManager: Boolean,
}

type Product {
  name: String,
  description: String,
  image: String,
  price: Number,
}

type Order {
  validate() { this.products.length > 0 }

  products: OrderItem[],
  shippingAddress: String,
  status: String,
}

type OrderItem {
  validate() { this.quantity > 0 }

  product: Product,
  quantity: Number,
}

path /users {
  read()  { isAdmin() }
  write() { false }
}

path /users/{uid} is User {
  create() { isCurrentUser(uid) || isAdmin() }
  read()   { isCurrentUser(uid) || isAdmin() }
  update() { isCurrentUser(uid) || isAdmin() }
  delete() { isAdmin() }
}

path /users/{uid}/email {
  update() { false }
}

path /roles {
  read()  { isAdmin() }
  write() { false }
}

path /roles/{uid} is Role {
  read()  { isAdmin() || isCurrentUser(uid) }
  write() { isAdmin() }
}

path /products {
  read()  { true }
  write() { false }
}

path /products/{id} is Product {
  create() { isManager() || isAdmin() }
  read()   { true }
  update() { isManager() || isAdmin() }
  delete() { isAdmin() }
}

path /cart/{uid} is Order {
  create() { isCurrentUser(uid) }
  read()   { isCurrentUser(uid) }
  update() { isCurrentUser(uid) }
  delete() { isCurrentUser(uid) || isAdmin() }
}

path /orders/{uid} {
  delete() { isAdmin() }
}

path /orders/{uid}/{id} is Order {
  create() { isCurrentUser(uid) }
  read()   { isCurrentUser(uid) || isManager() || isAdmin() }
  update() { false }
}

path /orders/{uid}/{id}/status {
  create() { isCurrentUser(uid) }
  read()   { isCurrentUser(uid) || isManager() }
  update() { isManager() }
  delete() { false }
}

isCurrentUser(uid) { auth !== null && auth.uid === uid }

isAdmin()   { auth !== null && prior(root.roles[auth.uid].isAdmin)   }
isManager() { auth !== null && prior(root.roles[auth.uid].isManager) }
